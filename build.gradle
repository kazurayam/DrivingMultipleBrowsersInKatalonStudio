// FlaskrTestInKatalonStudio

// $ gradle drivers


group = 'com.kazurayam'
version = '0.4.2'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    junit4ksVersion = '1.6.1'
    subprocessjVersion = '0.3.3'

    UITESTJAVA_PROJECT_DIR = '../../github/MyPythonProjectTemplate/uitestjava'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
}

dependencies {
    myconf group: 'com.kazurayam', name: 'junit4ks', version: "${junit4ksVersion}"
    myconf group: 'com.kazurayam', name: 'subprocessj', version: "${subprocessjVersion}"
}

// -----------------------------------------------------------------
// codes to import external jar files into the Drivers directory
import java.nio.file.Path
import java.nio.file.Paths

def downloadFile = { remoteUrl, localDir = 'Drivers' ->
    Path outFile = Paths.get(localDir).resolve("${AUTO_IMPORTED_JAR_PREFIX}${remoteUrl.tokenize('/')[-1]}")
    outFile.toFile().withOutputStream { out ->
        new URL(remoteUrl).withInputStream { from ->
            out << from
        }
    }
    println "downloaded ${remoteUrl} into ${localDir} directory as ${outFile.getFileName()}"
}


task drivers {
	doFirst {
	    delete fileTree("Drivers") {
	        include("**/*.jar")
	    }
	}
	doLast {
	    copy { copySpec ->
	        copySpec
	            .from(project.getConfigurations().getByName("myconf"))
	            .into("Drivers")
	            .include(
                        "**/junit4ks*.jar",
                        "**/subprocessj*.jar"
	                )
	            .rename({ s ->
	                AUTO_IMPORTED_JAR_PREFIX + s
	                })
	    }
    }
}

/**
 * This task mports the Java source code from the [MyPythonProjectTemplate/uitestjava](https://github.com/kazurayam/MyPythonProjectTemplate/tree/develop/uitestjava) project.
 * The java code includes the "Page Object Model" to test the Falskr web app.
 */
task importSourceOfFlaskrPOM {
    doLast {
        delete files("Include/scripts/groovy/com/kazurayam/uitestjava")
        copy { copySpec ->
            copySpec
                .from("${UITESTJAVA_PROJECT_DIR}/src/main/java")
                .into("Include/scripts/groovy")
                .include(
                    "com/kazurayam/uitestjava/**/*"
                )
        }
    }
}