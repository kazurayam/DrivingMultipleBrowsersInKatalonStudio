// FlaskrTestInKatalonStudio

// $ gradle drivers


group = 'com.kazurayam'
version = '0.2.0'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    junit4ksVersion = '1.6.1'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
}

dependencies {
  myconf group: 'com.kazurayam', name: 'junit4ks', version: "${junit4ksVersion}"
}

// -----------------------------------------------------------------
// codes to import external jar files into the Drivers directory
import java.nio.file.Path
import java.nio.file.Paths

def downloadFile = { remoteUrl, localDir = 'Drivers' ->
    Path outFile = Paths.get(localDir).resolve("${AUTO_IMPORTED_JAR_PREFIX}${remoteUrl.tokenize('/')[-1]}")
    outFile.toFile().withOutputStream { out ->
        new URL(remoteUrl).withInputStream { from ->
            out << from
        }
    }
    println "downloaded ${remoteUrl} into ${localDir} directory as ${outFile.getFileName()}"
}


task drivers {
	doFirst {
	    delete fileTree("Drivers") {
	        include("**/*.jar")
	    }
	}
	doLast {
	    copy { copySpec ->
	        copySpec
	            .from(project.getConfigurations().getByName("myconf"))
	            .into("Drivers")
	            .include(
	                "**/junit4ks*.jar"
	                )
	            .rename({ s ->
	                AUTO_IMPORTED_JAR_PREFIX + s
	                })
	    }
    }
}
