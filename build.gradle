// FlaskrTestInKatalonStudio

// $ gradle drivers


group = 'com.kazurayam'
version = '0.1.0'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
    webdriverfactoryVersion = '0.2.0-SNAPSHOT'
    webdriversupport4ksVersion = '0.4.0-SNAPSHOT'
    junit4ksVersion = '1.6.1'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    myconf
}

dependencies {
  myconf group: 'com.kazurayam', name: 'junit4ks', version: "${junit4ksVersion}"
  // I thought I would need the webdriverefactory library, but in the end, I didn't
  // myconf group: 'com.kazurayam', name: 'webdriverfactory', version: "${webdriverfactoryVersion}"
  }

// -----------------------------------------------------------------
// codes to import external jar files into the Drivers directory
import java.nio.file.Path
import java.nio.file.Paths

def downloadFile = { remoteUrl, localDir = 'Drivers' ->
    Path outFile = Paths.get(localDir).resolve("${AUTO_IMPORTED_JAR_PREFIX}${remoteUrl.tokenize('/')[-1]}")
    outFile.toFile().withOutputStream { out ->
        new URL(remoteUrl).withInputStream { from ->
            out << from
        }
    }
    println "downloaded ${remoteUrl} into ${localDir} directory as ${outFile.getFileName()}"
}


task drivers {
	doFirst {
	    delete fileTree("Drivers") {
	        include("**/*.jar")
	    }
	}
	doLast {
	    copy { copySpec ->
	        copySpec
	            .from(project.getConfigurations().getByName("myconf"))
	            .into("Drivers")
	            .include(
	                "**/webdriverfactory*.jar",
	                "**/junit4ks*.jar"
	                )
	            .rename({ s ->
	                AUTO_IMPORTED_JAR_PREFIX + s
	                })
	    }
        downloadFile("https://github.com/kazurayam/WebDriverSupport4ks/releases/download/${webdriversupport4ksVersion}/webdriversupport4ks-${webdriversupport4ksVersion}.jar")
    }
}
